buildscript {
	ext {
		springBootVersion = '2.0.3.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

group = 'foo.bar'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

// give test dependencies access to compileOnly dependencies to emulate providedCompile
configurations {
    testImplementation.extendsFrom compileOnly
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-actuator')
	implementation('org.springframework.boot:spring-boot-starter-web')
	compile('org.apache.commons:commons-lang3')
	implementation('org.projectlombok:lombok')
	implementation('org.springframework.boot:spring-boot-starter-test')
}

jacocoTestCoverageVerification.dependsOn jacocoTestReport
jacocoTestReport.dependsOn test

jacoco {
	toolVersion = '0.8.1'
}

jacocoTestReport {
	reports {
		xml.enabled false
		csv.enabled false
		html.enabled true
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.9
			}
		}
	}
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
	from javadoc.destinationDir
	classifier = 'javadoc'
}
task packageSources(type: Jar, dependsOn: 'classes') {
	from sourceSets.main.allSource
	classifier = 'sources'
}

jar {
	enabled = true
}
javadoc {
	failOnError = false
}

publish.dependsOn jacocoTestCoverageVerification
publishToMavenLocal.dependsOn jacocoTestCoverageVerification

publishing {
	publications {
		maven(MavenPublication) {
			groupId = 'foo.bar'
			artifactId = 'foo-bar'

			artifacts = [packageJavadoc, packageSources]
			from components.java

			pom {
				name = 'foo-bar'
				description = 'Foo Bar Projcet Description'
				url = 'https://github.com/Sarvesh-D/foo-bar'
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						id = 'Sarvesh-D'
						name = 'Sarvesh Dubey'
						email = 'sarvesh.dubey@hotmail.com'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/Sarvesh-D/foo-bar.git'
					developerConnection = 'scm:git:ssh://github.com:Sarvesh-D/foo-bar.git'
					url = 'http://github.com/Sarvesh-D/foo-bar/tree/master'
				}
			}
		}
	}

	repositories {
		mavenLocal()
	}
}
